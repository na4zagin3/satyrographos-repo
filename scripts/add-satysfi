#!/usr/bin/env ocaml
(* install shexp *)
(* TODO: pass -noinit to ocaml toplevel *)
#use "topfind"
#warnings "+a"
#thread
(* #require "ppx_jane,core" *)
#require "shexp.process"
module List' = List
open Shexp_process
open Shexp_process.Infix
(* open Core *)

let packages_dir = Sys.getcwd () ^ "/packages"
let resources_dir = Sys.getcwd () ^ "/resources"
let satysfi_dir = Sys.getcwd () ^ "/tmp/satysfi"

let satysfi_repo_url = "https://github.com/gfngfn/SATySFi.git"
let satysfi_dist_template = resources_dir ^ "/satysfi-dist.template"

let get_satysfi branch =
  file_exists (satysfi_dir ^ "/.git")
  >>= (function
    | false -> run "git" ["clone"; satysfi_repo_url; satysfi_dir]
    | true -> return ())
  >> chdir satysfi_dir (run "git" ["checkout"; branch] >> run "git" ["pull"])

let get_commit_hash branch =
  (* Run at satysfi_dir *)
  run "git" ["show"; "-s"; "--format=%H"; branch]
  |> capture_unit [Std_io.Stdout]
  >>| String.trim

let get_commit_datetime branch =
  (* Run at satysfi_dir *)
  run "git" ["show"; "-s"; "--format=%ci"; branch]
  |> capture_unit [Std_io.Stdout]
  >>| String.trim

let datetime_to_date datetime =
  match String.split_on_char ' ' datetime with
  | d :: _ -> d
  | _ -> failwith "Expect datetime in space separated format"

let construct_version original_version tag date = match tag with
  | None -> return original_version
  | Some tag ->
    echo (original_version ^ tag ^ date)
    |- (run "tr" ["\\055"; "."] |> capture_unit [Std_io.Stdout])
    >>| String.trim

let gen_url_section hash =
  String.concat "\n"
    [  "url {";
      {|  git: "git+https://github.com/gfngfn/SATySFi.git#|} ^ hash ^ {|"|};
       "}";
    ]

let get_version_from_opam_file opam =
  stdin_from opam (
    run "sed" ["-e"; {|/^version:/!d|}; "-e"; {|s/.*"\(.*\)".*/\1/|}]
    |> capture_unit [Std_io.Stdout]
    >>| String.trim )

let get_version_from_git_repo =
  run "git" ["describe";]
  |> capture_unit [Std_io.Stdout]
  >>| (fun s ->
      let len = String.length s in
      if len > 0 && (String.get s 0 = 'v')
      then String.sub s 1 (len - 1)
      else s
    )
  >>| String.trim

type schema =
  | Date
  | GitDescribe

let get_version schema branch override_version tag =
  get_commit_datetime branch >>= fun datetime ->
  let date = datetime_to_date datetime in
  match schema with
  | Date ->
    get_version_from_opam_file "satysfi.opam" >>= (fun original_version ->
        let version = Option.value ~default:original_version override_version in
        construct_version version tag date)
  | GitDescribe ->
    get_version_from_git_repo >>= fun version ->
    let tag =
      Option.value ~default:"" tag
    in
    return (version ^ tag)

let copy_satysfi version hash =
  let satysfi_package_dir version =
    packages_dir ^ "/satysfi/satysfi." ^ version in
  mkdir ~p:() (satysfi_package_dir version)
  >> stdout_to (satysfi_package_dir version ^ "/opam") (
    stdin_from "satysfi.opam" (
      run "sed" ["-e"; {|/^version:/d|}; "-e"; {|/^name:/d|}])
    >> echo (gen_url_section hash))

let echo_line x = echo ?n:None x

let copy_satysfi_dist version hash =
  let satysfi_dist_package_dir version =
    packages_dir ^ "/satysfi-dist/satysfi-dist." ^ version in
  mkdir ~p:() (satysfi_dist_package_dir version)
  >> stdout_to (satysfi_dist_package_dir version ^ "/opam") (
    stdin_from satysfi_dist_template (iter_lines echo_line)
    >> echo (gen_url_section hash))

let create_satysfi schema branch override_version tag =
  let command =
    get_version schema branch override_version tag >>= (fun version ->
        get_commit_hash branch >>= fun hash ->
        copy_satysfi version hash >>
        copy_satysfi_dist version hash) in
  get_satysfi branch
  >> chdir satysfi_dir command

let option_override_version = ref None
let option_date_schema = ref false
let option_branch = ref "master"
let option_version_tag = ref None
let specs =
  [
    ("-o", Arg.String(fun version -> option_override_version := Some version), "Override SATySFi version (default: none)");
    ("-t", Arg.String(fun tag -> option_version_tag := Some tag), "Version tag (default: none)");
    ("-b", Arg.Set_string(option_branch), "Branch name");
    ("-d", Arg.Set(option_date_schema), "Use date format schema");
  ]
let usage =
  {|USAGE: add-satysfi -b <branch>

OPTIONS:
 -o <version> Override the SATySFi version. (default: no-override)
 -b <branch> Branch name. (default: master)
 -t <tag> Version tag name (e.g., ~dev in satysfi.0.0.4+dev2020.02.16). (default: <branch>)
 -d Use date schema (e.g., satysfi.0.0.5+dev2020.09.05) instead of git describe (e.g., satysfi.0.0.5-27-gc841df+dev).
|}

let _ =
  Arg.parse specs (fun _ -> failwith usage) usage;
  let schema =
    if !option_date_schema
    then Date
    else GitDescribe
  in
  let c = create_satysfi schema (!option_branch) (!option_override_version) (!option_version_tag)
  in
  Traced.eval_exn c
